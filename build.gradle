buildscript {
    repositories {
        maven { url 'http://repo.spring.io/plugins-release' }
    }
    dependencies {
        classpath 'org.springframework.build.gradle:propdeps-plugin:0.0.6'
    }
}

apply plugin: 'propdeps'
apply plugin: 'propdeps-maven'
apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

if (JavaVersion.current().isJava8Compatible()) {
    allprojects {
        tasks.withType(Javadoc) {
            options.addStringOption('Xdoclint:none', '-quiet')
        }
    }
}


version = 1.1
sourceCompatibility = 1.6

task writeNewPom << {
    pom {
        project {
            modelVersion version
            groupId 'org.baswell'
            artifactId 'layouts'
            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }
        }
    }.writeTo("$buildDir/newpom.xml")
}

signing {
    sign configurations.archives
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

install {
    repositories.mavenInstaller {
        pom.version = version
        pom.groupId = 'org.baswell'
        pom.artifactId = 'layouts'
    }
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'Layouts'
                packaging 'jar'
                description 'JSP based layouts.'
                version version
                groupId 'org.baswell'
                artifactId 'layouts'
                url 'https://github.com/baswerc/layouts'

                scm {
                    url 'https://github.com/baswerc/layouts.git'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'baswerc'
                        name 'Corey Baswell'
                        email 'corey.baswell@gmail.com'
                    }
                }
            }
        }
    }
}

dependencies {
    provided 'javax.servlet:servlet-api:2.4'
    provided 'javax.servlet:jsp-api:2.0'
    testCompile group: 'junit', name: 'junit', version: '[4.0,)'
}